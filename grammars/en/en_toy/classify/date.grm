import '../byte.grm' as b;
import '../util.grm' as u;

d = b.kDigit;
q = u.q;
# The weight is to override the analysis of "Jan." etc. as a separate word (see
# word.grm).
month_file = StringFile['months.tsv'] <-20>;

# Allows both abbreviations and full names:
month = month_file | Project[month_file, 'output'];

# Any number from 1-31:

day = (d - "0") | "1" d | "2" d | "30" | "31";

# Any four digit number beginning with 1 or 2

year = ("1" | "2") d{3};

# Maps input of the form
#
# Jan. 3,? 1980
#
# or
#
# 3 Jan.,? 1980
#
# into
#
# date { month: "January" day: "3" year: "1980" }
#
# Etc.

mdy =
  u.I["date { "]
  u.I["month: " q]
  month
  u.D[" "+]
  u.I[q " day: " q]
  day
  u.D[","]?
  u.D[" "+]
  u.I[q " year: " q]
  year
  u.I[q]
  u.I[" preserve_order: true"]
  u.I[" }"];

dmy =
  u.I["date { "]
  u.I["day: " q]
  day
  u.D[" "+]
  u.I[q " month: " q]
  month
  u.D[","]?
  u.D[" "+]
  u.I[q " year: " q]
  year
  u.I[q]
  u.I[" preserve_order: true"]
  u.I[" }"];

export DATE = Optimize[mdy | dmy];
